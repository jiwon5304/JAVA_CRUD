plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.19.2'
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // database drivers
    runtimeOnly 'mysql:mysql-connector-java'

    // open-api
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.9'
    swaggerCodegen 'org.openapitools:openapi-generator-cli:6.0.0'
    compileOnly 'io.swagger.core.v3:swagger-annotations:2.2.1'
    compileOnly 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.openapitools:jackson-databind-nullable:0.2.3'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

ext {
    openApiConfigLocation = "$rootDir/src/main/resources/docs".toString()
}

swaggerSources {
    def typeMappings = 'URI=URI'
    def importMappings = 'URI=java.net.URI'
    javacrud {
        def apiYaml = "${openApiConfigLocation}/openapi.yml"
        def configJson = "${openApiConfigLocation}/config.json"
        inputFile = file(apiYaml)
        def ignoreFile = file("${openApiConfigLocation}/.openapi-generator-ignore")
        code {
            language = 'spring'
            configFile = file(configJson)
            rawOptions = ['--ignore-file-override', ignoreFile, '--type-mappings',
                          typeMappings, '--import-mappings', importMappings] as List<String>
            components = [models: true, apis: true, supportingFiles: 'ApiUtil.java']
            dependsOn validation
        }
    }
}

compileJava.dependsOn swaggerSources.javacrud.code
